apply plugin: "signing"

configurations {
    pom
}

ext {
    snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots"
    releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
    localUrl = "file:/${project.projectDir}/artifacts"
}

ext.repoUrl = snapshotUrl

ext."signing.keyId" = 'FDEB5A45'
ext."signing.secretKeyRingFile" = "$System.env.BAMBOO_JAR_SIGNING_KEYRING"
ext."signing.password" = "$System.env.BAMBOO_JAR_SIGNING_PASSWORD"

if (ext.isReleaseVersion) {

    signing {
        sign configurations.archives
    }

    signing {
        sign configurations.pom
    }

    publishing {
        repositories {
            maven { 
                url repoUrl
                credentials {
                    username = "$System.env.SONATYPE_USERNAME"
                    password = "$System.env.SONATYPE_PASSWORD"
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact ('README.md') {
                    classifier = 'README'
                    extension  = 'md'
                }

                artifact javadocJar
                artifact sourcesJar

                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name 'fhe-core'
                        description 'Kryptnostic implementations of Fully Homomorphic Encryption'
                        url 'https://www.github.com/kryptnostic/fhe-core'
                        scm {
                            url 'https://www.github.com/kryptnostic/fhe-core'
                            connection 'https://www.github.com/kryptnostic/fhe-core'
                            developerConnection 'https://www.github.com/kryptnostic/fhe-core'
                        }
                        licenses {
                            license {
                                name 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International Public License'
                                url 'https://creativecommons.org/licenses/by-nc-sa/4.0/legalcode'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'geekbeast'
                                name 'Matthew Tamayo-Rios'
                                email 'matthew@kryptnostic.com'
                            }
                            developer {
                                id 'hubsmoke'
                                name 'Sina Iman'
                                email 'sina@kryptnostic.com'
                            }
                            developer {
                                id 'nickdhewitt'
                                name 'Nick Hewitt'
                                email 'nick@kryptnostic.com'
                            }
                        }
                    }
                }
            }
        }
    }
}